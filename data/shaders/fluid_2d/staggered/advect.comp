#version 460
#extension GL_EXT_scalar_block_layout : enable
#extension GL_GOOGLE_include_directive : enable
#extension GL_EXT_samplerless_texture_functions : enable

layout(local_size_x = 32, local_size_y = 32) in;

#include "../common_new.glsl"

layout(set = 1, binding = 0) uniform sampler2D vector_field_u;
layout(set = 2, binding = 0) uniform sampler2D vector_field_v;
layout(set = 3, binding = 1) uniform texture2D quantity;
layout(set = 4, binding = 2) uniform writeonly image2D quantity_out;
layout(set = 5, binding = 0) uniform sampler aSampler;

layout(push_constant) uniform Constants {
    vec4 cell_center;
    float time_sign;
};

vec2 loadVectorField(vec2 uv) {
    const vec4 cc = cell_center;
    vec2 gid = floor(uv * textureSize(quantity, 0));
    uv = vec2(gid + cc.xy)/textureSize(vector_field_u, 0);
    float u = texture(vector_field_u, uv).x;

    uv = vec2(gid + cc.zw)/textureSize(vector_field_v, 0);
    float v = texture(vector_field_v, uv).x;

    return vec2(u, v);
}


vec4 getQuantity(vec2 xy) {
    return texture(sampler2D(quantity, aSampler), xy);
}

void updateQuantity(vec4 q) {
    imageStore(quantity_out, gid, q);
}

void main() {
    if(outOfBounds()) return;

    vec2 uv = (vec2(gid) + 0.5)/textureSize(quantity, 0);
    vec2 u = loadVectorField(uv);

    vec2 pm = uv - 0.5 * time_sign * dt * u;
    u = loadVectorField(pm);

    vec2 p = uv - time_sign * dt * u;
    vec4 q = getQuantity(p);

    updateQuantity(q);
}