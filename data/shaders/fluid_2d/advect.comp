#version 460
#extension GL_EXT_scalar_block_layout : enable
#extension GL_GOOGLE_include_directive : enable

layout(local_size_x = 32, local_size_y = 32) in;

layout(set = 0, binding = 0) uniform Globals{
    ivec2 gridSize;
    vec2 dx;
    vec2 dy;
    float dt;
    int ensureBoundaryCondition;
};

#include "common.glsl"

layout(set = 1, binding = 0) uniform sampler2D velocity_field_u;
layout(set = 1, binding = 1) uniform sampler2D velocity_field_v;

layout(set = 2, binding = 0) uniform texture2D quantity;
layout(set = 3, binding = 0) uniform sampler linerSampler;

layout(set = 4, binding = 0) uniform writeonly image2D quantityOut;

void main() {
    ivec2 gid = ivec2(gl_GlobalInvocationID);
    if(gid.x >= gridSize.x || gid.y >= gridSize.y) return;

    vec2 uv = (vec2(gl_GlobalInvocationID) + 0.5)/gridSize;

    vec2 u;
    u.x = texture(velocity_field_u, uv).x;
    u.y = texture(velocity_field_v, uv).x;

    vec2 p = st(uv - dt * u);
    vec4 q = texture(sampler2D(quantity, linerSampler), p);

    imageStore(quantityOut, gid, q);

}